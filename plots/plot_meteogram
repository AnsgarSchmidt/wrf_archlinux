begin
  cdf = readAsciiTable(input, 19, "float", 1)
  head = str_split(readAsciiHead(input, 1), " ")
  pressure   = cdf(:, 9) / 100
  rain_cum   = cdf(:, 16)
  rain_expl  = cdf(:, 17)
  tempht     = cdf(:, 5) - 273.15
  hum        = cdf(:, 6)
  taus       = cdf(:, 1)
  u          = cdf(:, 7)
  v          = cdf(:, 8)

  ; open output file
  wks = gsn_open_wks("png","meteo") ; send graphics to PNG file
  ; define color array
  colors = (/ (/255,255,255/),  \
             (/255,255,255/),   (/255,255,255/),   (/240,255,240/),  \
             (/220,255,220/),   (/190,255,190/),   (/120,255,120/),  \
             (/ 80,255, 80/),   (/ 50,200, 50/),   (/ 20,150, 20/) /) / 255.


  ; initialize value resources
  rain_res    = True
  rainsum_res = True
  tempsfc_res = True
  pres_res    = True
  wind_res    = True
  windd_res   = True
  hum_res     = True

  ; generate data arrays
  rel_hum = new(dimsizes(taus), double, 0)
  dew_point = new(dimsizes(taus), double, 0)
  w_speed = new(dimsizes(taus), double, 0)
  w_direction = new(dimsizes(taus), double, 0)
  spl = new(dimsizes(taus), double, 0)
  
  ; define constants
  r2d = 180.0d/(atan(1)*4.0d)
  r_air = 8.3144598
  g = 9.80665
  m = 0.0289644
  t_standard = 288.15
  z = todouble(head(13))

  ; create 3 hour timesteps
  timesteps = toint(taus(dimsizes(taus) - 1) / 3)
  print(timesteps)
  time_stamp = new(timesteps, integer)
  time_stamp(0) = 3
  
  ; initialize rain data
  rain_prev = 0.0
  rain_act = rain_cum(0) + rain_expl(0)
  rain03 = new(timesteps, double, 0)
  
  j = 0

  ; fill data arrays
  do i = 0, dimsizes(taus) - 1
    ; wind speed and wind direction
    w_speed(i) = sqrt(u(i) * u(i) + v(i) * v(i))
    w_direction(i) = atan2(u(i), v(i)) * r2d + 180

    ; sea level pressure
    spl(i) = pressure(i) * exp((m * g * z)/(r_air * t_standard))

    ; dew point
    saturation_pressure = 6.1094 * exp((17.685 * tempht(i)) / (tempht(i) + 243.04))
    partial_pressure = 0.622 * saturation_pressure / pressure(i)
    rel_hum_pre = hum(i) * 100 / partial_pressure
    rel_hum(i) = min((/rel_hum_pre, 100./))
    if (rel_hum(i) .eq. 100) then
      dew_point(i) = tempht(i)
    else
      dew_point(i) = tempht(i) - ((100. - rel_hum(i))/ 5.0)
    end if

    ; precipitation
    if (taus(i) .gt. time_stamp(j)) then
      rain_prev = rain_act
      rain_act = rain_cum(i) + rain_expl(i)
      rain03(j) = rain_act - rain_prev
      j = j + 1
      time_stamp(j) = time_stamp(j-1) + 3
    end if
  end do
  rain03(timesteps - 1) = rain_act - rain_prev

  ; rain sum line chart preparations
  rainsum_res@vpXF            = 0.15   ; The left side of the box location
  rainsum_res@vpYF            = 0.3    ; The top side of the plot box location
  rainsum_res@vpWidthF        = 0.75   ; The Width of the plot box
  rainsum_res@vpHeightF       = 0.10   ; The height of the plot box
  rainsum_res@trYAxisType = "IrregularAxis"
  rainsum_res@xyYIrregularPoints = (/0,1,2,3,4,8,16,32/)
  rainsum_res@trYMinF         = 0.0    ; min value on y-axis
  rainsum_res@trYMaxF         = 32     ; max value on y-axis
  rainsum_res@trXMinF         = 0.0    ; min value on x-axis
  rainsum_res@trXMaxF         = taus(dimsizes(taus) - 1)   ; max value on x-axis

  rainsum_res@tiXAxisString  = ""                ; X axis label.
  rainsum_res@tiYAxisString  = "3hr (mm)"  ; Y axis label.
  rainsum_res@tmXTOn         = False             ; turn off the top tickmarks
  rainsum_res@tmXBMode       = "Explicit"
  rainsum_res@tmXBValues     = ticks
  rainsum_res@tmXBLabels     = time_array
  rainsum_res@tmXBMinorValues = sticks
  rainsum_res@tmXMajorGrid = True
  rainsum_res@tmXMajorGridLineDashPattern = 2
  rainsum_res@tmYLMode       = "Explicit"
  rainsum_res@tmYLValues     = (/0,2,4,10,30/)
  rainsum_res@tmYLLabels     = (/0,2,4,10,30/)
  rainsum_res@tmYLMinorValues = (/1,3,5,20/)

  rainsum_res@tmYMajorGrid = True
  rainsum_res@xyLineThicknesses = 2          ; increase line thickness
  rainsum_res@gsnDraw         = False            ; Don't draw individual plot.
  rainsum_res@gsnFrame        = False            ; Don't advance frame.
  rainsum_res@gsnYRefLine     = 0.0              ; create a reference line
  rainsum_res@xyLineColor    =  "blue"        ; set line color

  ; 3 hr sum bar charts
  rain_res@trXMaxF         = taus(dimsizes(taus) - 1)   ; max value on x-axis
  rain_res@trYMaxF         = rain_cum + rain_expl   ; max value on y-axis
  rain_res@trYMinF         = -.1  ; min value on y-axis

  rain_res@tiXAxisString  = ""            ; turn off x-axis string
  rain_res@tmXTOn         = False         ; turn off the top tickmarks
  rain_res@xyLineThicknesses = 2          ; increase line thickness
  rain_res@gsnAboveYRefLineColor = "green"    ; above ref line fill green
  rain_res@gsnXYBarChart   = True             ; turn on bar chart
  rain_res@tmYLMaxTicks = 6

  rain_res@gsnDraw         = False        ; Don't draw individual plot.
  rain_res@gsnFrame        = False        ; Don't advance frame.

  ; temperature / dew point at surface
  tempsfc_res@vpXF            = 0.15   ; The left side of the box location
  tempsfc_res@vpYF            = 0.15   ; The top side of the plot box loc
  tempsfc_res@vpWidthF        = 0.75   ; The Width of the plot box
  tempsfc_res@vpHeightF       = 0.10   ; The height of the plot box
  tempsfc_res@trXMaxF         = taus(dimsizes(taus) - 1)   ; max value on x-axis
  tempsfc_res@trYMaxF         = max(tempht)+0.5   ; max value on y-axis
  tempsfc_res@trYMinF         = min(dew_point)-0.5   ; min value on y-axis

  tempsfc_res@tiXAxisString  = ""            ; turn off x-axis string
  tempsfc_res@tiYAxisString  = "T / Td (~S~o~N~C)"  ; set y-axis string
  tempsfc_res@tmXTOn         = False             ; turn off the top tickmarks
  tempsfc_res@tmXBMode       = "Explicit"
  tempsfc_res@tmXBValues     = ticks
  tempsfc_res@tmXBLabels     = time_array
  tempsfc_res@tmXBMinorValues = sticks
  tempsfc_res@tmXMajorGrid = True
  tempsfc_res@tmXMajorGridLineDashPattern = 2

  tempsfc_res@xyLineThicknesses = 2          ; increase line thickness
  tempsfc_res@xyLineColor    =  "red"        ; set line color
  tempsfc_res@tmYMajorGrid = True
  tempsfc_res@tmYLMaxTicks = 6

  tempsfc_res@gsnDraw         = False        ; Don't draw individual plot.
  tempsfc_res@gsnFrame        = False        ; Don't advance frame.

  temptmsz  = gsn_csm_xy(wks,taus,tempht,tempsfc_res)
  tempsfc_res@xyLineColor    =  "blue"        ; set line color
  dewpmsz  = gsn_csm_xy(wks,taus,dew_point,tempsfc_res)

  ; relative humidity at surface
  hum_res@vpXF            = 0.15   ; The left side of the box location
  hum_res@vpYF            = 0.75   ; The top side of the plot box loc
  hum_res@vpWidthF        = 0.75   ; The Width of the plot box
  hum_res@vpHeightF       = 0.10   ; The height of the plot box
  hum_res@trXMaxF         = taus(dimsizes(taus) - 1)  ; max value on x-axis
  hum_res@trYMaxF         = 100    ; max value on y-axis
  hum_res@trYMinF         = 0.0    ; min value on y-axis
  hum_res@gsnYRefLine = 0.1

  hum_res@tiXAxisString  = ""            ; turn off x-axis string
  hum_res@tiYAxisString  = "RH (%)"      ; set y-axis string
  hum_res@tmXTOn         = False         ; turn off the top tickmarks
  hum_res@tmXBMode       = "Explicit"
  hum_res@tmXBValues     = ticks
  hum_res@tmXBLabels     = time_array
  hum_res@tmXBMinorValues = sticks
  hum_res@tmXMajorGrid = True
  hum_res@tmXMajorGridLineDashPattern = 2

  hum_res@xyLineThicknesses = 2          ; increase line thickness
  hum_res@gsnAboveYRefLineColor = "dark green"
  hum_res@xyLineColor    =  "dark green"        ; set line color
  hum_res@tmYMajorGrid = True

  hum_res@gsnDraw         = False        ; Don't draw individual plot.
  hum_res@gsnFrame        = False        ; Don't advance frame.

  ; wind speed at surface
  wind_res@vpXF            = 0.15   ; The left side of the box location
  wind_res@vpYF            = 0.45   ; The top side of the plot box loc
  wind_res@vpWidthF        = 0.75   ; The Width of the plot box
  wind_res@vpHeightF       = 0.10   ; The height of the plot box
  wind_res@trXMaxF         = taus(dimsizes(taus) - 1)   ; max value on x-axis
  wind_res@trYMaxF         = max(w_speed)+1   ; max value on y-axis
  wind_res@trYMinF         = 0.0   ; min value on y-axis

  wind_res@tiXAxisString  = ""            ; turn off x-axis string
  wind_res@tiYAxisString  = "uv2m (m/s)"; set y-axis string
  wind_res@tmXTOn         = False         ; turn off the top tickmarks
  wind_res@tmXBMode       = "Explicit"
  wind_res@tmXBValues     = ticks
  wind_res@tmXBLabels     = time_array
  wind_res@tmXBMinorValues = sticks
  wind_res@tmXMajorGrid = True
  wind_res@tmXMajorGridLineDashPattern = 2
  wind_res@tmYLMaxTicks = 6

  wind_res@xyLineThicknesses = 2          ; increase line thickness
  wind_res@xyLineColor    =  "green"        ; set line color

  wind_res@gsnDraw         = False        ; Don't draw individual plot.
  wind_res@gsnFrame        = False        ; Don't advance frame.

  ; wind direction at surface
  windd_res@vpXF            = 0.15   ; The left side of the box location
  windd_res@vpYF            = 0.6   ; The top side of the plot box loc
  windd_res@vpWidthF        = 0.75   ; The Width of the plot box
  windd_res@vpHeightF       = 0.10   ; The height of the plot box
  windd_res@trXMaxF         = taus(dimsizes(taus) - 1)   ; max value on x-axis
  windd_res@trYMaxF         = 360.0   ; max value on y-axis
  windd_res@trYMinF         = 0.0   ; min value on y-axis

  windd_res@tiXAxisString  = ""            ; turn off x-axis string
  windd_res@tiYAxisString  = "dir"; set y-axis string
  windd_res@tmXTOn         = False         ; turn off the top tickmarks
  windd_res@tmYLMode       = "Explicit"
  windd_res@tmYLValues     = (/0, 90, 180, 270/)
  windd_res@tmYLLabels     = (/"N", "E", "S", "W"/)
  windd_res@tmYLMinorValues = (/ 45, 135, 225, 315/)
  windd_res@tmXBMode       = "Explicit"
  windd_res@tmXBValues     = ticks
  windd_res@tmXBLabels     = time_array
  windd_res@tmXBMinorValues = sticks
  windd_res@tmXMajorGrid = True
  windd_res@tmXMajorGridLineDashPattern = 2

  windd_res@xyLineThicknesses = 2          ; increase line thickness
  windd_res@xyMarkLineMode = "Markers"
  windd_res@xyMarkers      = 10
  windd_res@xyMarkerColor  = "purple"
  windd_res@xyMarkerSizeF  = 0.001

  windd_res@gsnDraw         = False        ; Don't draw individual plot.
  windd_res@gsnFrame        = False        ; Don't advance frame.

  ; sea level pressure
  pres_res@vpXF            = 0.15   ; The left side of the box location
  pres_res@vpYF            = 0.9   ; The top side of the plot box loc
  pres_res@vpWidthF        = 0.75   ; The Width of the plot box
  pres_res@vpHeightF       = 0.10   ; The height of the plot box
  pres_res@trXMaxF         = taus(dimsizes(taus) - 1)   ; max value on x-axis
  pres_res@trYMaxF         = max(spl)+1   ; max value on y-axis
  pres_res@trYMinF         = min(spl)-1   ; min value on y-axis
  pres_res@tiMainString    = str_sub_str(title,"_"," ")

  pres_res@tiXAxisString  = ""            ; turn off x-axis string
  pres_res@tiYAxisString  = "p (hPa)"  ; set y-axis string
  pres_res@tmXTOn         = False             ; turn off the top tickmarks
  pres_res@tmXBMode       = "Explicit"
  pres_res@tmXBValues     = ticks
  pres_res@tmXBLabels     = time_array
  pres_res@tmXBMinorValues = sticks
  pres_res@tmXMajorGrid = True
  pres_res@tmXMajorGridLineDashPattern = 2
  pres_res@tmYLMaxTicks = 6

  pres_res@xyLineThicknesses = 2          ; increase line thickness
  pres_res@xyLineColor       =  "blue"        ; set line color
  pres_res@tmYMajorGrid      = True

  pres_res@gsnDraw         = False        ; Don't draw individual plot.
  pres_res@gsnFrame        = False        ; Don't advance frame.

  pressmsz  = gsn_csm_xy(wks,taus,spl,pres_res)

  ; generate plot results
  rainhist  = gsn_csm_xy(wks,time_stamp,rain03,rain_res)
  rainsum  = gsn_csm_xy(wks,taus,rain_cum + rain_expl,rainsum_res)
  windmsz  = gsn_csm_xy(wks,taus,w_speed,wind_res)
  winddmsz  = gsn_csm_xy(wks,taus,w_direction,windd_res)
  hummsz  = gsn_csm_xy(wks,taus,rel_hum,hum_res)

  ; draw plots a given locations
  draw(pressmsz)             ; draw pressure time series
  overlay(temptmsz, dewpmsz)
  draw(temptmsz)             ; draw the temperature time series
  overlay(rainsum, rainhist)
  draw(rainsum)             ; draw the rain histogram
  draw(windmsz)
  draw(winddmsz)
  draw(hummsz)
  frame(wks)                 ; finally advance the frame once all on page
end
