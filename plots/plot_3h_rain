load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/gsn_code.ncl"
load "$NCARG_ROOT/lib/ncarg/nclscripts/wrf/WRFUserARW.ncl"

begin
  FILES = systemfunc (" ls -1 " + "wrfout* ")
  numFILES = dimsizes(FILES)
  print("numFILES = " + numFILES)
  print(FILES)
  print (" ")

; set basic resources
  res = True
  res@MainTitle = "WRF: 3h precipitation"

  pltres = True                      ; plotting resources
  mpres = True                       ; map resources
  mpres@mpGeophysicalLineColor = "Black"
  mpres@mpGeophysicalLineThicknessF = "2"
  mpres@mpFillBoundarySets = "AllBoundaries"
  mpres@mpNationalLineColor    = "Black"
  mpres@mpGridLineColor        = "Black"
  mpres@mpLimbLineColor        = "Black"
  mpres@mpPerimLineColor       = "Black"
  mpres@mpDataBaseVersion      = "HighRes"

; prepare files and data
  a = addfiles(FILES+".nc","r")

  times = wrf_user_getvar(a,"times",-1)  ; get all times in the file
  ntimes = dimsizes(times)           ; number of times in the file

  slp = wrf_user_getvar(a,"slp",-1)  ; sea level pressure
  wrf_smooth_2d( slp, 3 )            ; smooth sea level pressure

  rain_tot = wrf_user_getvar(a,"RAINNC",-1) + wrf_user_getvar(a,"RAINC",-1)
  rain_tot@description = "Total Precipitation"

  ; just creating new arrays here
  rain_tot_tend = rain_tot
  rain_tot_tend = 0.0
  rain_tot_tend@description = "3h Precipitation"

  ; Calculate tendency values
  ; We know our data is available every 3 hours, so setting tend_int to 1 gives
  ; tendencies for 3 hours, setting it to 2, gives 6hourly tendencies
  tend_int = 1
  rain_tot_tend(tend_int:ntimes-1:tend_int,:,:) = rain_tot(tend_int:ntimes-1:tend_int,:,:) - rain_tot(0:ntimes-2:tend_int,:,:)

; start loop to generare all output images
  do it = tend_int,ntimes-1,tend_int    ; Start at index 1 since there is no information for the previous interval
    wks_type = "png"
    wks_type@wkWidth = 2500
    wks_type@wkHeight = 2500
    time_array = str_split(times(it), ":")
    wks = gsn_open_wks(wks_type,"rain_3h_" + time_array(0))

    print("Working on time: " + times(it) )
    res@TimeLabel = times(it)   ; Set the timestamp of the actual index

  ; Plotting options for Sea Level Pressure
    opts_slp = res
    opts_slp@ContourParameters = (/ 900., 1100., 4. /)
    opts_slp@cnLineColor       = "Gray25"
    opts_slp@cnInfoLabelOn     = True
    opts_slp@cnHighLabelsOn = True                           ; Set labels
    opts_slp@cnLowLabelsOn = True
    opts_slp@cnLineLabelFontHeightF = 0.01
    opts_slp@cnLineLabelPerimOn = False
    opts_slp@gsnContourLineThicknessesScale = 1.5
    contour_psl = wrf_contour(a[it],wks,slp(it,:,:),opts_slp)
    delete(opts_slp)

  ; Plotting options for Precipitation
    opts_r = res
    opts_r@UnitLabel            = "mm"
    opts_r@cnLevelSelectionMode = "ExplicitLevels"
    opts_r@cnLevels             = (/ .1, .2, .4, .8, 1.6, 3.2, 6.4, \
                                    12.8, 25.6, 51.2, 102.4, 204.8/)
    opts_r@cnFillColors         = (/"White","White","DarkOliveGreen1", \
                                "DarkOliveGreen3","Chartreuse", \
                                    "Chartreuse3","Green","ForestGreen", \
                                    "Yellow","Orange","Red","Violet","Blue"/)
    opts_r@cnInfoLabelOn        = False
    opts_r@cnConstFLabelOn      = False
    opts_r@cnFillOn             = True
    delete(opts_r)

  ; generate output image for current index
    pltres@PanelPlot = True
    pltres@FramePlot = False
    contour_tend = wrf_contour(a[it],wks, rain_tot_tend(it,:,:),opts_r) ; total (color)
    plot = wrf_map_overlays(a[it],wks,(/contour_tend,contour_psl/),pltres,mpres)
    draw(plot)
    frame(wks)

  end do

end

