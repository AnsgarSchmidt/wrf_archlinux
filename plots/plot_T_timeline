;   Example script - read and plot data from WRF time series output
;   November 2008

load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/gsn_code.ncl"
load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/gsn_csm.ncl"
load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/contributed.ncl"

begin

type = "png"
type@wkWidth = 1250
type@wkHeight = 1000
wks = gsn_open_wks(type,"time_T2")
data = readAsciiTable(input, 19, "float", 1)   ; read the data file
taus       = data(:, 1)
tempht     = data(:,5)-273.15
hum        = data(:, 6)
pressure   = data(:, 9) / 100

rel_hum = new(dimsizes(taus), double, 0)
dew_point = new(dimsizes(taus), double, 0)
do i = 0, dimsizes(taus) - 1
  saturation_pressure = 6.1094 * exp((17.685 * tempht(i)) / (tempht(i) + 243.04))
  partial_pressure = 0.622 * saturation_pressure / pressure(i)
  rel_hum_pre = hum(i) * 100. / partial_pressure
  rel_hum(i) = min((/rel_hum_pre, 100./))
  if (rel_hum(i) .eq. 100) then
    dew_point(i) = tempht(i)
  else
    dew_point(i) = tempht(i) - ((100. - rel_hum(i))/ 5.0)
  end if
end do

res = True
res@tmXTOn         = False
res@xyLineThicknesses = 2
res@xyLineColor    =  "red"
res@tiYAxisString = "2m T / Td (~S~o~N~C)"
res@tiMainString = str_sub_str(title,"_"," ")
res@gsnDraw          = False                    ; don't draw plot yet
res@gsnFrame         = False                    ; don't advance frame yet
res@vpWidthF         = 0.75                      ; set width and height
res@vpHeightF        = 0.3
res@trXMaxF         = taus(dimsizes(taus) - 1)   ; max value on x-axis
res@trYMaxF         = max(tempht)+0.5   ; max value on y-axis
res@trYMinF         = min(dew_point)-0.5   ; min value on y-axis
res@tmXBMode       = "Explicit"
res@tmXBValues     = ticks
res@tmXBLabels     = time_array
res@tmXBMinorValues = sticks
res@tmXMajorGrid = True
res@tmXMajorGridLineDashPattern = 2
res@tmYMajorGrid = True

res@gsnMaximize = True

temptmsz  = gsn_csm_xy(wks,taus,tempht,res)    ; column 2 is time and column 6 is T2
res@xyLineColor    =  "blue"        ; set line color
dewpmsz  = gsn_csm_xy(wks,taus,dew_point,res)
overlay(temptmsz, dewpmsz)
draw(temptmsz)
psres = True
maximize_output(wks,psres)

end

